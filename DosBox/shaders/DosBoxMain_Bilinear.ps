#version 100

precision highp float;
precision mediump int;

uniform highp sampler2D texUnit;
uniform vec4 sourceSize;
uniform highp sampler2D palUnit;
uniform vec2 one;
uniform vec2 uvRatio;

varying vec2 tcNorm;


// PAL() takes an 8-bit value as a U texture coordinate and does
// a lookup into the palette texture to return RGB values for that index
// 
#define PAL(p)	 			texture2D(palUnit, vec2((p),0.5))
#define LOOKUP(c)			PAL(texture2D(texUnit, (c) * uvRatio).x)


void main() {

	vec2 texelPos = (tcNorm * sourceSize.xy) - vec2(0.5);

	vec2 texelPosFrac = fract(texelPos);
	vec2 halfPixel = one * vec2(0.5);

	vec3 texel00 = LOOKUP(tcNorm).xyz;
	vec3 texel10 = LOOKUP(tcNorm + vec2( one.x,   0)).xyz;
	vec3 texel01 = LOOKUP(tcNorm + vec2( 0,            one.y)).xyz;
	vec3 texel11 = LOOKUP(tcNorm + vec2( one.x,  one.y)).xyz;

	vec3 yBlend0 = mix(texel00, texel01, texelPosFrac.y);
	vec3 yBlend1 = mix(texel10, texel11, texelPosFrac.y);
	vec3 xBlend = mix(yBlend0, yBlend1, texelPosFrac.x);
	
	gl_FragColor.xyz = xBlend;
	gl_FragColor.w = 1.0;
}
